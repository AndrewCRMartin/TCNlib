#!/bin/csh
#
# asurf - run the accessibility program  
#
#  Script written by Simon Hubbard, EMBL: 1994
#
#  27.05.94 Added -c (contact) handlng    By: ACRM
#  22.01.95 improved usage message        By: ACRM
#  17.05.96 Added -b (big probe) handling By: ACRM
#           Added Stuart Moodie's mods for multiple runs

# echo "Current host name = " $HOST

set nargs = $#argv

if ( $nargs < 1 ) then
   echo "Usage: asurf pdb_file -p probe_size -r vdw_radii_file -z zslice -[hwcb]"
   echo "-p probe radius (A)"
   echo "-z integration accuracy"
   echo "-r VDR Radii file"
   echo "-h Include HETATMs"
   echo "-w Include waters"
   echo "-c Calculate contact rather than accessible surface"
   echo "-b Run a version of accall optimised for big probes"
   exit(1)
endif    
#
# options:
#      -p = probe size (next arg probe size)
#      -z = accuracy (next arg is accuracy)
#      -r = vdw radii file (next arg is filename)
#      -h = hetatoms ?
#      -w = waters ?
#      -c = contact (rather than accessible) surface
#
set hets = 0
set wats = 0
set contact = 0
set PDBFILE = 0
set VDWFILE = 0
set probe = 1.40
set zslice = 0.05
set EXE_PATH = /acrm/home/andrew/sa
set program = accall64


while ( $#argv )
  switch ($argv[1])
     case -[pP]:	
	shift 	
	set probe = $argv[1]
	breaksw
     case -[zZ]:
        shift
        set zslice = $argv[1]
	breaksw
     case -[hH]:
        set hets = 1
	breaksw
     case -[wW]:
        set wats = 1
        breaksw
     case -[rR]:
        shift
        set VDWFILE = $argv[1]
	breaksw
     case -[cC]:
        set contact = 1
        breaksw
     case -[bB]:
        set program = accall_bigprobe
        breaksw
     default:
        if ( -e $argv[1] && $PDBFILE == 0 ) then
	   set PDBFILE = $argv[1]
        endif
	breaksw
  endsw
  shift
end

if ( $PDBFILE == 0 ) then
  echo "you must supply a pdb format file"
  exit(1)
endif

if ( $VDWFILE == 0 ) then
  set VDWFILE = "$EXE_PATH/vdw.radii"
endif

set com = $0
set tf = /tmp/$com:t.$$

echo "PDBFILE $PDBFILE" >! $tf
echo "VDWFILE $VDWFILE" >> $tf
echo "PROBE $probe"     >> $tf
echo "ZSLICE $zslice"   >> $tf
if ( $hets ) then
  echo "HETATOMS"       >> $tf
endif
if ( $wats ) then
  echo "WATERS"         >> $tf
endif
if ( $contact ) then
  echo "CONTACT"        >> $tf
endif

echo "Running accall"

$EXE_PATH/$program < $tf

\rm $tf
